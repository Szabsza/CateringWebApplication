apply plugin: 'war'

dependencies {
    compileOnly group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.0.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.16.0-rc1'
    implementation group: 'com.github.jknack', name: 'handlebars', version: '4.3.1'
    implementation project(':csim2126-backend')
}

def deploymentName = 'csim2126-web'
def tomcatHome = System.getenv('CATALINA_HOME')

logger.quiet("Tomcat home detected as '${tomcatHome}'")

task deploy(type: Copy, dependsOn: 'war') {
    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }

    from war.archiveFile

    rename war.archiveFileName.get(), "${deploymentName}.war"

    into "${tomcatHome}/webapps"
}

task undeploy(type: Delete) {
    doFirst {
        if (!tomcatHome) {
            throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
        }
    }

    delete "${tomcatHome}/webapps/${deploymentName}.war"
}